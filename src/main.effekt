module main
import src/lib
import io/console


record TimeEntry(startTime: String, endTime: String)
record Project(name: String, times: List[TimeEntry])

effect startTimer(): Unit
effect stopTimer(): Unit
effect quitMainLoop(): Unit
effect switchProject(project: Project): Unit

type TimerState {
  Stopped()
  Running()
}

def iterateTimeEntries(entries: List[TimeEntry]): Unit =
  entries match {
    case Nil() => println("Thats it!")
    case Cons(a, rest) => {
      println(a.startTime)
      println(a.endTime)

      iterateTimeEntries(rest)
    }
  }


def handleInput(state: TimerState): Unit / { startTimer, stopTimer, quitMainLoop } = {
  with console;
  val input = do readLine()

  if (input == "s") {
    do startTimer()
  }
  else if (input == "p") {
    do stopTimer()
  }
  else if (input == "q") {
    do quitMainLoop()
  }
}


def mainLoop(): Unit = {
  var active: Bool = true
  var state: TimerState = Stopped()
  var project: Project = Project("none", [])
  var startTime: String = ""
  var endTime: String = ""
  
  while (active) {
    try{
      handleInput(state)
    } with startTimer {
      println("Timer started")
      state = Running()
      startTime = getCurrentTime()
      resume(())
    } with stopTimer {
      println("Timer stopped")
      state = Stopped()
      endTime = getCurrentTime()
      val timeEntry = TimeEntry(startTime, endTime)
      project = Project(project.name, project.times.append([timeEntry]))
      resume(())
    } with quitMainLoop {
      active = false
    }
  }

  iterateTimeEntries(project.times)
  println(calcTimeDiffInSecs(startTime, endTime))
}

def main(): Unit = {
  println("Hey! Welcome to effect time tracker!")
  println("Press 's' to start time tracking")
  println("Press 'p' to stop")
  println("Press 'q' to quit")

  mainLoop()

  println("Goodbye!")
}